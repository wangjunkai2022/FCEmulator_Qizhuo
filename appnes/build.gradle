apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
ext {
    isCopyNesZipToAssets = project.hasProperty('addRom')
    //这里表示是否压缩rom包到安装包中 使用命令 ./gradlew assembleRelease -PaddRom
}
android {
    compileSdkVersion 30

    signingConfigs {
        demokey {
            storeFile file('../fcemulator.jks')
            storePassword "123456"
            keyAlias 'fcmulatorkey'
            keyPassword '123456'
        }

    }
    defaultConfig {
        // applicationId 'nostalgia.appnes'
        applicationId 'com.qizhuo.fcnes'
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1000
        versionName "1.0.00"
        resConfigs "zh", "en"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64" // 默认情况下可用的 ABI
        }
//        //分包ABI后大小河原始差距不大 不用分包
//        splits {
//            abi {
//                enable true         // 启用分 ABI 功能
//                reset()             // 重置设置
//                include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'  // 包含的 ABI
//            }
//        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
                version "3.20.0"
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    repositories {
        maven {
            url "https://dl.bintray.com/ironsource-mobile/android-sdk"
        }
        mavenCentral()
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.demokey
            externalNativeBuild {
                cmake {
                    cppFlags.add('-DNATIVE_LOG')
                }
            }
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.demokey

        }
    }

    if (isCopyNesZipToAssets) {
        println "开启了创建Rom到 assets!"

        // 自定义任务：压缩文件夹并复制到 assets
        task zipFolderToAssets(type: Zip) {
            println "创建Rom到 assets!"
            // 指定要压缩的文件夹路径
            from('../ROM') {
                include '**/*.nes' // 递归查找任何子目录中的 .nes 文件
            }
            // 指定输出 ZIP 文件的位置
            archiveFileName = 'Classified.zip'
            destinationDirectory = file('src/main/assets/gamezip/')
        }

        // 在 assemble 任务前执行
        tasks.whenTaskAdded { task ->
            if (task.name == ('assembleRelease')) {
                println "开始执行了 创建Rom到 assets!"
                def zipFile = file('src/main/assets/gamezip/Classified.zip')
                if (zipFile.exists()) {
                    println "存在不需要创建了"
                } else {
                    task.dependsOn(zipFolderToAssets)
                    // 确保 zipFolderToAssets 在 assembleRelease 之前运行
                    println "已将 zipFolderToAssets 任务添加为 ${task.name} 的依赖."
                }
            }else if (task.name.startsWith('package')) {
                println "task.name：${task.name}"
                task.mustRunAfter(zipFolderToAssets)
            }
        }

        // 让 zipFolderToAssets 任务在 assemble 任务执行后运行
        zipFolderToAssets.doLast {
            println "ZIP 文件已成功创建并复制到 assets!"
        }
    } else {
        // 当 isCopyNesZipToAssets 为 false 时，检查并删除指定文件夹
        def gameZipDir = file('src/main/assets/gamezip')

        if (gameZipDir.exists() && gameZipDir.isDirectory()) {
            gameZipDir.deleteDir() // 删除目录及其内容
            println "目录 'gamezip' 已被删除."
        } else {
            println "目录 'gamezip' 不存在，无需删除."
        }
    }

    //原生代码调试符号文件
//    android.buildTypes.release.ndk.debugSymbolLevel = { SYMBOL_TABLE | FULL }
//    applicationVariants.all { variant ->
//        variant.outputs.all {
//            def fileName = project.name + '-' + variant.name + '-V' +
//                    defaultConfig.versionCode + ".apk"
//            outputFileName = fileName
//        }
//    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':framework')
    implementation 'com.google.firebase:firebase-storage:19.2.1'
    implementation 'com.karumi:dexter:6.2.2'
    implementation "com.tonyodev.fetch2:fetch2:3.0.12"
//    implementation 'org.apache.commons:commons-compress:1.21' // 请根据需要选择合适的版本
}